import org.gradle.api.Project
import org.gradle.api.initialization.Settings

apply plugin: 'war'
apply plugin: 'project-report'
apply from: "${rootProject.projectDir}/libraries.gradle"

buildscript {
    repositories {
        maven { url 'http://repo.jfrog.org/artifactory/gradle' }
    }
}

dependencies {
    subprojects.each { Project project -> runtime project }
}

configurations {
    all*.exclude module: "groovy-all"
    all*.exclude module: "insight-annotation"
}

allprojects {
    repositories {
        mavenCentral()
        maven { url "http://repo.springsource.org/libs-release" }
        flatDir name: 'localRepository', dirs: "${rootProject.rootDir}/lib"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply from: "${rootProject.projectDir}/tool/checkstyle/config/checkstyle.gradle"
    apply from: "${rootProject.projectDir}/tool/thucydides/config/thucydides.gradle"
    apply from: "${rootProject.projectDir}/tool/jacoco/config/jacoco.gradle"


    dependencies {
        compile libraries.'insight-annotation'
        compile libraries.'slf4j-api'
        compile libraries.'spring-context'
        compile libraries.'commons-lang3'
        compile libraries.'guava'

        groovy libraries.'groovy-all-indy'
        testCompile libraries.'fest-assert'
        testCompile libraries.'mockito-all'
        testCompile libraries.'mockito-groovy-support'
        testCompile libraries.'testng'
        testCompile libraries.'thucydides-core'
        testCompile libraries.'thucydides-jbehave'

        runtime libraries.'logback'
    }

    /* invoke dynamic config */
    def enableIndy = System.properties.enableIndy?.toBoolean() ?: false
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
    [compileGroovy.groovyOptions,compileTestGroovy.groovyOptions]*.with {
        fork = true
        optimizationOptions = [indy: enableIndy, 'int': true]
        encoding = 'UTF-8'
    }

    test {
        testLogging.showStandardStreams = false

        /* turn off Gradle's HTML report to avoid replacing the reports generated by TestNG library */
        testReport = false
        useTestNG() {
            /* report generation delegated to TestNG library */
            useDefaultListeners = true

        }
    }

    compileJava.dependsOn clean
    jar.dependsOn checkstyleReport
}

